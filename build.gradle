buildscript {
    ext.kotlin_version = "1.9.25"
    ext.jacocoVersion = '0.8.7'
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        if (System.getenv("SHOULD_PUBLISH") == "true") {
            classpath("io.github.gradle-nexus:publish-plugin:1.1.0")
        }
        classpath 'com.android.tools.build:gradle:8.11.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacocoVersion"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
}

sonarqube {
    properties {
        property "sonar.projectKey", "OutSystems_OSInAppBrowserLib-Android"
        property "sonar.organization", "outsystemsrd"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

if (System.getenv("SHOULD_PUBLISH") == "true") {
    apply plugin: "io.github.gradle-nexus.publish-plugin"
    apply from: file("./scripts/publish-root.gradle")
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "jacoco"

android {
    namespace "com.outsystems.plugins.inappbrowser.osinappbrowserlib"
    compileSdk 36

    defaultConfig {
        minSdk 26
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    sourceSets {
        getByName("main").java.srcDirs("src/main/java")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

        reports {
            xml.getRequired().set(true)
            html.getRequired().set(true)
        }

        def fileFilter = ['**/BuildConfig.*', '**/Manifest*.*']
        def debugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
        def mainSrc = "${project.projectDir}/src/main/java"

        sourceDirectories.setFrom(files([mainSrc]))
        classDirectories.setFrom(files([debugTree]))
        executionData.setFrom(fileTree(dir: "$buildDir", includes: [
                "jacoco/testDebugUnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ]))
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant("release")
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.webkit:webkit:1.12.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.activity:activity-ktx:1.9.3'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    //gson
    implementation 'com.google.code.gson:gson:2.10.1'


    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.12.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
}

if (System.getenv("SHOULD_PUBLISH") == "true") {
    apply from: file("./scripts/publish-module.gradle")
}

import java.net.URL

def pdfJsVersion = "5.4.54"
def pdfJsUrl = "https://github.com/mozilla/pdf.js/releases/download/v${pdfJsVersion}/pdfjs-${pdfJsVersion}-dist.zip"
def pdfJsDestDir = "$projectDir/src/main/assets/pdfjs"
def pdfJsCacheDir = new File(gradle.gradleUserHomeDir, "pdfjs-cache/${pdfJsVersion}")

tasks.register("downloadPdfJs") {
    description = "Downloads and caches PDF.js distribution files"
    group = "build setup"
    
    // Define inputs/outputs for up-to-date checking
    inputs.property("pdfJsVersion", pdfJsVersion)
    outputs.dir(pdfJsDestDir)
    
    doLast {
        def destDir = file(pdfJsDestDir)
        
        // Check if destination already has files
        if (destDir.exists() && destDir.listFiles()?.size() > 0) {
            logger.info("PDF.js files already exist in ${destDir}")
            return
        }

        // Check if cache has files and copy them
        if (pdfJsCacheDir.exists() && pdfJsCacheDir.listFiles()?.size() > 0) {
            logger.info("Copying PDF.js from cache: ${pdfJsCacheDir}")
            copy {
                from pdfJsCacheDir
                into destDir
            }
            return
        }

        // Download PDF.js
        logger.info("Downloading PDF.js v${pdfJsVersion} from ${pdfJsUrl}")
        def zipFile = file("$buildDir/pdfjs-${pdfJsVersion}.zip")
        
        try {
            // Ensure build directory exists
            zipFile.parentFile.mkdirs()
            
            // Download with better error handling
            new URL(pdfJsUrl).withInputStream { inputStream ->
                zipFile.withOutputStream { outputStream ->
                    outputStream << inputStream
                }
            }
            
            logger.info("Downloaded PDF.js to: ${zipFile}")

            // Extract to cache
            pdfJsCacheDir.mkdirs()
            copy {
                from zipTree(zipFile)
                into pdfJsCacheDir
            }

            // Copy from cache to destination
            if (destDir.exists()) {
                delete destDir
            }
            destDir.mkdirs()
            copy {
                from pdfJsCacheDir
                into destDir
            }
            
            logger.info("PDF.js extracted to: ${destDir}")
            
            // Clean up downloaded zip
            delete zipFile
            
        } catch (Exception e) {
            logger.error("Failed to download PDF.js: ${e.message}")
            throw new GradleException("Could not download PDF.js from ${pdfJsUrl}", e)
        }
    }
}

preBuild.dependsOn(downloadPdfJs)
